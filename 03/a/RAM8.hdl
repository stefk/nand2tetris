/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    DMux8Way(
        in=load,
        sel=address,
        a=loadR0,
        b=loadR1,
        c=loadR2,
        d=loadR3,
        e=loadR4,
        f=loadR5,
        g=loadR6,
        h=loadR7
    );
    Register(in=in, load=loadR0, out=r0Out);
    Register(in=in, load=loadR1, out=r1Out);
    Register(in=in, load=loadR2, out=r2Out);
    Register(in=in, load=loadR3, out=r3Out);
    Register(in=in, load=loadR4, out=r4Out);
    Register(in=in, load=loadR5, out=r5Out);
    Register(in=in, load=loadR6, out=r6Out);
    Register(in=in, load=loadR7, out=r7Out);
    Mux8Way16(
        a=r0Out,
        b=r1Out,
        c=r2Out,
        d=r3Out,
        e=r4Out,
        f=r5Out,
        g=r6Out,
        h=r7Out,
        sel=address,
        out=out
    );
}
